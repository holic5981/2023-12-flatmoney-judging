Orbiting Cyan Finch

high

# LimitOrder.cancelLimitOrder can be used to unlock position with LeverageClose order

## Summary

A position is an nft in the LeverageModule. This means that all positions are tradable. This is behavior expected by the protocol.

[[announceLeverageAdjust](https://github.com/sherlock-audit/2023-12-flatmoney/blob/main/flatcoin-v1/src/DelayedOrder.sol#L217-L223)](https://github.com/sherlock-audit/2023-12-flatmoney/blob/main/flatcoin-v1/src/DelayedOrder.sol#L217-L223)/[[announceLeverageClose](https://github.com/sherlock-audit/2023-12-flatmoney/blob/main/flatcoin-v1/src/DelayedOrder.sol#L317)](https://github.com/sherlock-audit/2023-12-flatmoney/blob/main/flatcoin-v1/src/DelayedOrder.sol#L317) are used to request adjusting or closing of specified position. In order to prevent nft from being transferred away during this process, the current implementation executes `leverageModule.lock(tokenId)` in both functions[[[1](https://github.com/sherlock-audit/2023-12-flatmoney/blob/main/flatcoin-v1/src/DelayedOrder.sol#L298)](https://github.com/sherlock-audit/[2](https://github.com/sherlock-audit/2023-12-flatmoney/blob/main/flatcoin-v1/src/DelayedOrder.sol#L361)023-12-flatmoney/blob/main/flatcoin-v1/src/DelayedOrder.sol#L298), [2](https://github.com/sherlock-audit/2023-12-flatmoney/blob/main/flatcoin-v1/src/DelayedOrder.sol#L361)]. Then, until the request(pending order) is executed or expires, the position cannot be traded because it is locked.

However, a malicious user can use [[LimitOrder.cancelLimitOrder](https://github.com/sherlock-audit/2023-12-flatmoney/blob/main/flatcoin-v1/src/LimitOrder.sol#L87)](https://github.com/sherlock-audit/2023-12-flatmoney/blob/main/flatcoin-v1/src/LimitOrder.sol#L87) to unlock the position regardless of whether it has a pending `LeverageClose`/`LeverageAdjust` order.

## Vulnerability Detail

We will give an example to describe the root and impact of this problem. Consider the following scenario:

```data
minExecutabilityAge is 5s  
maxExecutabilityAge is 60s
```

1. Bob has a position A, which is currently profitable and worth $10,000. Bob intends to sell the position at a 90% discount. Alice gets this and wants to buy the position. Therefore, Tom, as an intermediary, received $9000 USDC from Alice and promised to transfer $8100 USDC to Bob as soon as he noticed that position A was transferred to Alice's address, and $900 USDC (10%) served as Tom's commission.

2. Bob calls `announceLeverageClose` for position A, and a pending `LeverageClose` order is written to `_announcedOrder[bob]`. Position A is locked and cannot be transferred.

```solidity
File: flatcoin-v1\src\DelayedOrder.sol
317:     function announceLeverageClose(uint256 tokenId, uint256 minFillPrice, uint256 keeperFee) external whenNotPaused {
......
346:->       _announcedOrder[msg.sender] = FlatcoinStructs.Order({
347:             orderType: FlatcoinStructs.OrderType.LeverageClose,
348:             orderData: abi.encode(
349:                 FlatcoinStructs.AnnouncedLeverageClose({
350:                     tokenId: tokenId,
351:                     minFillPrice: minFillPrice,
352:                     tradeFee: tradeFee
353:                 })
354:             ),
355:             keeperFee: keeperFee,
356:             executableAtTime: executableAtTime
357:         });
358: 
359:         // Lock the NFT belonging to this position so that it can't be transferred to someone else.
360:         // Locking doesn't require an approval from the leverage trader.
361:->       leverageModule.lock(tokenId);
......
368:     }
```

3. Bob calls `LimitOrder.announceLimitOrder` for position A, and internally calls `LeverageModule.lock(tokenId)` to lock again. [[This does not affect](https://github.com/sherlock-audit/2023-12-flatmoney/blob/main/flatcoin-v1/src/misc/ERC721LockableEnumerableUpgradeable.sol#L32-L36)](https://github.com/sherlock-audit/2023-12-flatmoney/blob/main/flatcoin-v1/src/misc/ERC721LockableEnumerableUpgradeable.sol#L32-L36). Therefore, a pending LimitClose limit order is written to `_limitOrderClose[tokenId]`.

```solidity
File: flatcoin-v1\src\LimitOrder.sol
58:     function announceLimitOrder(uint256 tokenId, uint256 priceLowerThreshold, uint256 priceUpperThreshold) external {
59:         uint64 executableAtTime = _prepareAnnouncementOrder();
            //@audit check whether msg.sender is tokenId's owner.
60:->       address positionOwner = _checkPositionOwner(tokenId);
......
66:->       _limitOrderClose[tokenId] = FlatcoinStructs.Order({
67:             orderType: FlatcoinStructs.OrderType.LimitClose,
68:             orderData: abi.encode(
69:                 FlatcoinStructs.LimitClose(tokenId, priceLowerThreshold, priceUpperThreshold, tradeFee)
70:             ),
71:             keeperFee: 0, // Not applicable for limit orders. Keeper fee will be determined at execution time.
72:             executableAtTime: executableAtTime
73:         });
74: 
75:         // Lock the NFT belonging to this position so that it can't be transferred to someone else.
            //@audit locking again will not revert. It's safe.
76:->       ILeverageModule(vault.moduleAddress(FlatcoinModuleKeys._LEVERAGE_MODULE_KEY)).lock(tokenId);
......
83:     }
```

4. Bob calls `LimitOrder.cancelLimitOrder` for position A. Because bob is still the owner of the position at this time, the function is successfully executed, and `LeverageModule.unlock(tokenId)` is called internally to unlock position A.

```solidity
File: flatcoin-v1\src\LimitOrder.sol
87:     function cancelLimitOrder(uint256 tokenId) external {
88:->       address positionOwner = _checkPositionOwner(tokenId);
89:         _checkLimitCloseOrder(tokenId);
90: 
91:         delete _limitOrderClose[tokenId];
92: 
93:         // Unlock the ERC721 position NFT to allow for transfers.
            //@audit after unlock is called, tokenId is unlock. so it can be transferable.
94:->       ILeverageModule(vault.moduleAddress(FlatcoinModuleKeys._LEVERAGE_MODULE_KEY)).unlock(tokenId);
95: 
96:         emit FlatcoinEvents.OrderCancelled({account: positionOwner, orderType: FlatcoinStructs.OrderType.LimitClose});
97:     }
```

5. Bob transfers position A to Aliceâ€™s address.

**Note that steps 2-5 can be completed in one tx**.

6. Tom noticed the event and transferred $8100 USDC to bob.

7. After the time has passed `minExecutabilityAge`, the LeverageClose order generated in step 2 can be executed. Bob, as the keeper, calls `DelayedOrder.executeOrder(bob, priceUpdateData)`, and finally successfully closes position A and obtains the margin and profit of the position.

8. Alice paid $9000 USDC, and position A was finally burned. Therefore, she lost $9000 USDC.

**The reasons that caused Alice to suffer losses are**:

1.  The lock restriction is bypassed.
    
2.  In `DelayedOrder.executeOrder`, the account specified when closing a position is the initiator of the order and not the owner of the position NFT.
    
    ```solidity
    File: flatcoin-v1\src\DelayedOrder.sol
    619:     function _executeLeverageClose(address account) internal returns (int256 settledMargin) {
    620:         FlatcoinStructs.Order memory order = _announcedOrder[account];
    621: 
    622:         _prepareExecutionOrder(account, order.executableAtTime);
    623: 
    624:         settledMargin = ILeverageModule(vault.moduleAddress(FlatcoinModuleKeys._LEVERAGE_MODULE_KEY)).executeClose({
    625:->           account: account,	//@audit account = bob in this case.
    626:             keeper: msg.sender,
    627:             order: order
    628:         });
    629: 
    630:         emit FlatcoinEvents.OrderExecuted({account: account, orderType: order.orderType, keeperFee: order.keeperFee});
    631:     }
    ```
    
    At the end of `LeverageModule.executeClose`, we can see that [the address to receive funds is `account`](https://github.com/sherlock-audit/2023-12-flatmoney/blob/main/flatcoin-v1/src/LeverageModule.sol#L314).
    

## Impact

The above examples already describes the impact: The lock limit is bypassed and the position buyer suffers funds loss.

## Code Snippet

https://github.com/sherlock-audit/2023-12-flatmoney/blob/main/flatcoin-v1/src/LimitOrder.sol#L94

## Tool used

Manual Review

## Recommendation

First:

```fix
File: flatcoin-v1\src\DelayedOrder.sol
598:         ILeverageModule(vault.moduleAddress(FlatcoinModuleKeys._LEVERAGE_MODULE_KEY)).executeAdjust({
599:---          account: account,
599:+++          account: leverageModule.ownerOf(leverageAdjust.tokenId),
600:             keeper: msg.sender,
601:             order: order
602:         });

File: flatcoin-v1\src\DelayedOrder.sol
++++++       FlatcoinStructs.AnnouncedLeverageClose memory leverageClose = abi.decode(
++++++           order.orderData,
++++++           (FlatcoinStructs.AnnouncedLeverageClose)
++++++       );
624:         settledMargin = ILeverageModule(vault.moduleAddress(FlatcoinModuleKeys._LEVERAGE_MODULE_KEY)).executeClose({
625:---          account: account,
625:+++          account: leverageModule.ownerOf(leverageClose.tokenId),
626:             keeper: msg.sender,
627:             order: order
628:         });
```

Second: It is recommended that positions that have been locked cannot be locked again.